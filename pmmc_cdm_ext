drop program maur_tn_pmmc_cdm_ext:dba go
create program maur_tn_pmmc_cdm_ext:dba
 
 
/**************************************************************
; DVDev DECLARED SUBROUTINES
**************************************************************/
declare LoadOutAlias (s_code_set = vc, f_contrib_sys_cd = f8 ) = i2
declare GetAlias (f_cv = f8, f_contrib_sys_cd = f8 ) = vc
/**************************************************************
; DVDev DECLARED VARIABLES
**************************************************************/
declare ms_file_path = vc with protect, noconstant(cnvtlower(trim($1,3)))
declare ms_dt = vc with protect, constant(trim(format(cnvtdatetime(curdate,curtime3),'YYYYMMDD;;q'),3))
 
declare mf_cs73_HL7STDSRC_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",73,"HL7STDSRC"))
declare mf_cs319_FIN_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!2930'))
declare mf_cs18649_CHARGE_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!29774'))
declare mf_cs71_INPATIENT_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!3958'))
declare mf_cs71_SWINGBED_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"SWINGBED"))
declare mf_cs71_PREREG_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"PREREG"))
declare mf_cs71_CLIENT_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"CLIENT"))
 
declare mf_cs367_SELFPAY_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",367,"SELFPAY"))
 
declare mf_cs354_BCBS_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"BCBS"))
declare mf_cs354_COMMERCIALCONTRACT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"COMMERCIALCONTRACT"))
declare mf_cs354_MEDICAREREPLACEMENT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICAREREPLACEMENT"))
declare mf_cs354_MEDICAID_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICAID"))
declare mf_cs354_MRHEMPLOYEES_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MRHEMPLOYEES"))
declare mf_cs354_WORKERSCOMPENSATION_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"WORKERSCOMPENSATION"))
declare mf_cs354_MEDICARE_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICARE"))
declare mf_cs354_COMMERCIALNONCONTRACT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"COMMERCIALNONCONTRACT"))
 
 
declare mf_cs354_CLIENT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"CLIENT"))
declare mf_cs354_SELFPAY_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"SELFPAY"))
declare mf_cs20849_CLIENT_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!142832'))
 
declare ml_idx1 = i4 with protect, noconstant(0)
declare ml_idx2 = i4 with protect, noconstant(0)
/**************************************************************
; DVDev Start Coding
**************************************************************/
 
 
free record m_out_alias
record m_out_alias(
  1 l_cs_cnt = i4
  1 qual[*]
    2 f_code_set = f8
    2 l_cv_cnt = i4
    2 qual[*]
     3 f_code_value = f8
     3 s_alias = vc
     3 f_contrib_sys_cd = f8) with protect
 
free record m_rec
record m_rec(
  1 l_cnt = i4
  1 qual[*]
    2 f_person_id = f8
    2 f_encntr_id = f8
    2 f_pft_encntr_id = f8
    2 s_fin = vc
    2 s_med_service = vc
    2 s_bill_entity = vc
    2 f_health_plan_id = f8
    2 l_ch_cnt = i4
    2 chrg[*]
      3 f_charge_item_id = f8
      3 f_bill_item_id = f8
      3 f_amount = f8
      3 f_quanity = f8
      3 s_service_dt = vc
      3 s_cdm = vc
) with protect
 
 
free record frec
record frec(
  1 file_desc = i4
  1 file_offset = i4
  1 file_dir = i4
  1 file_name = vc
  1 file_buf = vc) with protect
 
;call LoadOutAlias("34", mf_cs73_HL7STDSRC_CD)
 
;call echorecord(m_out_alias)
 
set frec->file_name = concat(ms_file_path,"c_mrmc_chg_codes_",ms_dt,".txt")
set frec->file_buf = "w"
 
 
select into 'nl:'
  from coding cod,
       charge c,
       pft_charge pc,
       pft_encntr pe,
       pft_trans_reltn ptr,
       encounter e,
       person p,
       billing_entity be,
       benefit_order bo,
       bo_hp_reltn bhr,
       health_plan hp,
       encntr_alias ea,
       charge_mod cm,
       code_value cv
plan cod
  where cod.updt_dt_tm between cnvtdatetime(curdate-1,0) and cnvtdatetime(curdate-1,235959)
    and cod.active_ind = 1
    and cod.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
  ;  and cod.completed_dt_tm is not null
join e
  where e.encntr_id = cod.encntr_id
    and e.encntr_type_cd not in (mf_cs71_PREREG_CD, mf_cs71_CLIENT_CD)
join pe
  where pe.encntr_id = e.encntr_id
    and pe.active_ind = 1
    ;and pe.fin_class_cd in (mf_cs354_BCBS_CD, mf_cs354_COMMERCIALCONTRACT_CD, mf_cs354_MEDICAREREPLACEMENT_CD,
    ;                        mf_cs354_MEDICAID_CD, mf_cs354_MRHEMPLOYEES_CD,mf_cs354_WORKERSCOMPENSATION_CD,
    ;                        mf_cs354_MEDICARE_CD,mf_cs354_COMMERCIALNONCONTRACT_CD)
    and pe.fin_class_cd not in (mf_cs354_CLIENT_CD, mf_cs354_SELFPAY_CD)
    and exists (select a.acct_id from account a where a.acct_id = pe.acct_id
                   and a.active_ind = 1
                   and a.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
                   and a.acct_sub_type_cd != mf_cs20849_CLIENT_CD
                   )
join ptr
  where ptr.parent_entity_id = pe.pft_encntr_id
    and ptr.parent_entity_name = 'PFTENCNTR'
    and ptr.trans_type_cd = mf_cs18649_CHARGE_CD
join pc
  where pc.activity_id = ptr.activity_id
    and pc.pft_encntr_id = pe.pft_encntr_id
    and pc.active_ind = 1
    and pc.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join c
  where c.charge_item_id = pc.charge_item_id
    and c.active_ind = 1
    and c.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
;plan c
;  where c.posted_dt_tm between cnvtdatetime(curdate-1,0) and cnvtdatetime(curdate-1,235959)
;    and c.active_ind = 1
;    and c.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
;join pc
;  where pc.charge_item_id = c.charge_item_id
;   and pc.active_ind = 1
;    and pc.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
;join pe
;  where pe.pft_encntr_id = pc.pft_encntr_id
;join ptr
;  where ptr.parent_entity_id = pe.pft_encntr_id
;    and ptr.activity_id = pc.activity_id
;    and ptr.parent_entity_name = 'PFTENCNTR'
;    and ptr.trans_type_cd = mf_cs18649_CHARGE_CD
;join e
;  where e.encntr_id = pe.encntr_id
join p
  where p.person_id = e.person_id
join be
  where be.billing_entity_id = pe.billing_entity_id
    and be.active_ind = 1
    and be.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and be.be_name_key in ('MAURYREGIONALMEDICALCENTER','MARSHALLMEDICALCENTER','WAYNEMEDICALCENTER')
join bo
  where bo.pft_encntr_id = pe.pft_encntr_id
    and bo.active_ind = 1
    and bo.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join bhr
  where bhr.benefit_order_id = bo.benefit_order_id
    and bhr.active_ind = 1
    and bhr.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join hp
  where hp.health_plan_id = bhr.health_plan_id
    and hp.active_ind = 1
    and hp.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and hp.plan_type_cd != mf_cs367_SELFPAY_CD
join ea
  where ea.encntr_id = pe.encntr_id
    and ea.active_ind = 1
    and ea.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and ea.encntr_alias_type_cd = mf_cs319_FIN_CD
join cm
  where cm.charge_item_id = outerjoin(c.charge_item_id)
    and cm.active_ind = outerjoin(1)
    and cm.end_effective_dt_tm >= outerjoin(cnvtdatetime(curdate,curtime3))
join cv
  where cv.code_value = outerjoin(cm.field1_id)
    and cv.code_set = outerjoin(14002)
    and cv.active_ind = outerjoin(1)
    and cv.end_effective_dt_tm >= outerjoin(cnvtdatetime(curdate,curtime3))
    and cv.cdf_meaning = outerjoin('CDM_SCHED')
order by pe.pft_encntr_id, c.charge_item_id asc, cm.field1_id, cm.field2_id asc
head report
  m_rec->l_cnt = 0
head pe.pft_encntr_id
  m_rec->l_cnt = m_rec->l_cnt + 1
  stat = alterlist(m_rec->qual,m_rec->l_cnt)
  m_rec->qual[m_rec->l_cnt].f_encntr_id = pe.encntr_id
  m_rec->qual[m_rec->l_cnt].f_pft_encntr_id = pe.pft_encntr_id
  m_rec->qual[m_rec->l_cnt].f_person_id = p.person_id
 
  if (pe.recur_ind = 1)
    m_rec->qual[m_rec->l_cnt].s_fin = concat(trim(cnvtalias(ea.alias,ea.alias_pool_cd),3),'-',trim(cnvtstring(pe.recur_seq),3))
  else
    m_rec->qual[m_rec->l_cnt].s_fin = trim(cnvtalias(ea.alias,ea.alias_pool_cd),3)
  endif
 ; m_rec->qual[m_rec->l_cnt].s_med_service = getAlias(e.med_service_cd, mf_cs73_HL7STDSRC_CD)
 
  if (e.encntr_type_cd in (mf_cs71_INPATIENT_CD, mf_cs71_SWINGBED_CD))
    m_rec->qual[m_rec->l_cnt].s_med_service = 'I'
  else
    m_rec->qual[m_rec->l_cnt].s_med_service = 'O'
  endif
 
  if (be.be_name_key = 'MAURYREGIONALMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'MRH'
  elseif (be.be_name_key = 'MARSHALLMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'MMC'
  elseif (be.be_name_key = 'WAYNEMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'WMC'
  endif
  m_rec->qual[m_rec->l_cnt].f_health_plan_id = bo.health_plan_id
  m_rec->qual[m_rec->l_cnt].l_ch_cnt = 0
head c.charge_item_id
  m_rec->qual[m_rec->l_cnt].l_ch_cnt = m_rec->qual[m_rec->l_cnt].l_ch_cnt + 1
  stat = alterlist(m_rec->qual[m_rec->l_cnt].chrg,m_rec->qual[m_rec->l_cnt].l_ch_cnt)
  m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].f_charge_item_id = c.charge_item_id
  m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].f_bill_item_id = c.bill_item_id
  m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].f_amount = evaluate(ptr.dr_cr_flag,2,-1*ptr.amount,ptr.amount)
  m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].f_quanity =
                                                            evaluate(ptr.dr_cr_flag,2,-1*abs(c.item_quantity),abs(c.item_quantity))
  m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].s_service_dt = trim(format(c.service_dt_tm,'MM/DD/YYYY;;q'),3)
detail
  if (cv.cdf_meaning = 'CDM_SCHED')
    if (size(trim(m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].s_cdm,3)) = 0)
      m_rec->qual[m_rec->l_cnt].chrg[m_rec->qual[m_rec->l_cnt].l_ch_cnt].s_cdm = trim(cm.field6,3)
    endif
  endif
with nocounter
 
;call echorecord(m_rec)
 
 
;open file
set stat = cclio("OPEN",frec)
 
;write header row
 
set frec->file_buf = build('"Patient ID",'
                        ,'"Charge Code",'
                        ,'"Charges",'
                        ,'"Units/Surgical Units",'
                        ,'"",'
                        ,'"",'
                        ,'"",'
                        ,'"Service Category",'
                        ,'"Provider Org/Group Code",'
                        ,'"Service Date"'
                        ,char(13),char(10))
set stat = cclio("WRITE",frec)
 
 
;write data
for (ml_idx1 = 1 to m_rec->l_cnt)
  for (ml_idx2 = 1 to m_rec->qual[ml_idx1].l_ch_cnt)
    set frec->file_buf = build('"',m_rec->qual[ml_idx1].s_fin
                              ,'","',m_rec->qual[ml_idx1].chrg[ml_idx2].s_cdm
                              ,'","',trim(cnvtstring(m_rec->qual[ml_idx1].chrg[ml_idx2].f_amount,20,2),3)
                              ,'","',trim(cnvtstring(m_rec->qual[ml_idx1].chrg[ml_idx2].f_quanity),3)
                              ,'","'
                              ,'","'
                              ,'","'
                              ,'","',m_rec->qual[ml_idx1].s_med_service
                              ,'","',m_rec->qual[ml_idx1].s_bill_entity
                              ,'","',m_rec->qual[ml_idx1].chrg[ml_idx2].s_service_dt
                              ,'"',char(13),char(10))
    set stat = cclio("WRITE",frec)
  endfor
endfor
 
;close file
set stat = cclio("CLOSE",frec)
 
/**************************************************************
; DVDev DEFINED SUBROUTINES
**************************************************************/
subroutine GetAlias(f_cv, f_contrib_sys_cd)
  declare ml_cv_pos = i4 with noconstant(0)
  declare ml_cs_pos = i4 with noconstant(0)
  declare ml_idx = i4 with noconstant(0)
  declare mf_code_set = f8 with noconstant(0)
  declare s_ret_val = vc with noconstant(concat("CD::",trim(cnvtstring(f_cv,20),3)))
 
  set mf_code_set = uar_get_code_set(f_cv)
 
  if(mf_code_set > 0)
    set ml_cs_pos = locatevalsort(ml_idx,1,m_out_alias->l_cs_cnt,mf_code_set,m_out_alias->qual[ml_idx].f_code_set)
  else
    return(s_ret_val)
  endif
 
  set ml_cv_pos = locatevalsort(ml_idx,1,m_out_alias->qual[ml_cs_pos].l_cv_cnt, f_cv,
                  m_out_alias->qual[ml_cs_pos].qual[ml_idx].f_code_value, f_contrib_sys_cd,
                  m_out_alias->qual[ml_cs_pos].qual[ml_idx].f_contrib_sys_cd)
 
  if(ml_cv_pos > 0)
    set s_ret_val = trim(m_out_alias->qual[ml_cs_pos].qual[ml_cv_pos].s_alias,3)
  endif
 
  return(s_ret_val)
end
 
subroutine LoadOutAlias(s_code_set, f_contrib_sys_cd)
  declare l_ret_val = i2 with protect, noconstant(0)
  ;declare l_cs_cnt = i4 with protect, noconstant(0)
  declare l_cv_cnt = i4 with protect, noconstant(0)
 
  if(f_contrib_sys_cd > 0.0)
    select into "nl:"
      from code_value_outbound cvo
     where parser(build("cvo.code_set in (",s_code_set,")"))
       and cvo.contributor_source_cd = f_contrib_sys_cd
    order by cvo.code_set, cvo.code_value
   ; head report
   ;   m_out_alias->l_cs_cnt = 0
    head cvo.code_set
      m_out_alias->l_cs_cnt = m_out_alias->l_cs_cnt + 1
      stat = alterlist(m_out_alias->qual,m_out_alias->l_cs_cnt)
      m_out_alias->qual[m_out_alias->l_cs_cnt].f_code_set = cvo.code_set
      m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt = 0
    detail
      m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt = m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt  + 1
      stat = alterlist(m_out_alias->qual[m_out_alias->l_cs_cnt]->qual,m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt)
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].f_code_value = cvo.code_value
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].s_alias = cvo.alias
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].f_contrib_sys_cd =
                                                                                                           cvo.contributor_source_cd
    with nocounter
 
    if(m_out_alias->l_cs_cnt > 0)
      set l_ret_val = 1
    endif
  endif
  return(l_ret_val)
end
 
 
#EXIT_SCRIPT
 
end
go
 
