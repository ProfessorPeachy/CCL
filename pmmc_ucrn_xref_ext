drop program maur_tn_pmmc_ucrn_xref_ext:dba go
create program maur_tn_pmmc_ucrn_xref_ext:dba
 
 
/**************************************************************
; DVDev DECLARED SUBROUTINES
**************************************************************/
declare LoadOutAlias (s_code_set = vc, f_contrib_sys_cd = f8 ) = i2
declare GetAlias (f_cv = f8, f_contrib_sys_cd = f8 ) = vc
/**************************************************************
; DVDev DECLARED VARIABLES
**************************************************************/
declare ms_file_path = vc with protect, noconstant(cnvtlower(trim($1,3)))
declare ms_dt = vc with protect, constant(trim(format(cnvtdatetime(curdate,curtime3),'YYYYMMDD;;q'),3))
 
declare mf_cs73_HL7STDSRC_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",73,"HL7STDSRC"))
declare mf_cs319_FIN_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!2930'))
declare mf_cs21849_CLAIM_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!461985'))
 
declare mf_cs71_INPATIENT_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!3958'))
declare mf_cs71_SWINGBED_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"SWINGBED"))
declare mf_cs71_PREREG_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"PREREG"))
declare mf_cs71_CLIENT_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",71,"CLIENT"))
 
declare mf_cs367_SELFPAY_CD  = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",367,"SELFPAY"))
 
declare mf_cs354_BCBS_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"BCBS"))
declare mf_cs354_COMMERCIALCONTRACT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"COMMERCIALCONTRACT"))
declare mf_cs354_MEDICAREREPLACEMENT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICAREREPLACEMENT"))
declare mf_cs354_MEDICAID_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICAID"))
declare mf_cs354_MRHEMPLOYEES_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MRHEMPLOYEES"))
declare mf_cs354_WORKERSCOMPENSATION_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"WORKERSCOMPENSATION"))
declare mf_cs354_MEDICARE_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"MEDICARE"))
declare mf_cs354_COMMERCIALNONCONTRACT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"COMMERCIALNONCONTRACT"))
 
declare mf_cs354_CLIENT_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"CLIENT"))
declare mf_cs354_SELFPAY_CD = f8 with protect, constant(uar_get_code_by("DISPLAYKEY",354,"SELFPAY"))
declare mf_cs20849_CLIENT_CD = f8 with protect, constant(uar_get_code_by_cki('CKI.CODEVALUE!142832'))
 
/**************************************************************
; DVDev Start Coding
**************************************************************/
 
 
free record m_out_alias
record m_out_alias(
  1 l_cs_cnt = i4
  1 qual[*]
    2 f_code_set = f8
    2 l_cv_cnt = i4
    2 qual[*]
     3 f_code_value = f8
     3 s_alias = vc
     3 f_contrib_sys_cd = f8) with protect
 
free record m_rec
record m_rec(
  1 l_cnt = i4
  1 qual[*]
    2 s_ucrn = vc
    2 f_encntr_id = f8
    2 f_pe_encntr_id = f8
    2 s_fin = vc
    2 s_med_service = vc
    2 s_bill_entity = vc
  ) with protect
 
 
free record frec
record frec(
  1 file_desc = i4
  1 file_offset = i4
  1 file_dir = i4
  1 file_name = vc
  1 file_buf = vc) with protect
 
;call LoadOutAlias("34", mf_cs73_HL7STDSRC_CD)
 
;call echorecord(m_out_alias)
 
set frec->file_name = concat(ms_file_path,"c_mrmc_ucrn_",ms_dt,".txt")
set frec->file_buf = "w"
 
select into 'nl:'
  from bill_rec br,
       bill_reltn brt,
       bo_hp_reltn bhr,
       health_plan hp,
       benefit_order bo,
       pft_encntr pe,
       billing_entity be,
       encntr_alias ea,
       encounter e
plan br
  where br.submit_dt_tm between cnvtdatetime(curdate-1,0) and cnvtdatetime(curdate-1,235959)
    and br.active_ind = 1
    and br.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and br.bill_class_cd = mf_cs21849_CLAIM_CD
join brt
  where brt.corsp_activity_id = br.corsp_activity_id
    and brt.parent_entity_name = 'BO_HP_RELTN'
    and brt.active_ind = 1
    and brt.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join bhr
  where bhr.bo_hp_reltn_id = brt.parent_entity_id
    and bhr.active_ind = 1
    and bhr.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join hp
  where hp.health_plan_id = bhr.health_plan_id
    and hp.active_ind = 1
    and hp.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and hp.plan_type_cd != mf_cs367_SELFPAY_CD
join bo
  where bo.benefit_order_id = bhr.benefit_order_id
    and bo.active_ind = 1
    and bo.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
join pe
  where pe.pft_encntr_id = bo.pft_encntr_id
    and pe.active_ind = 1
   ; and pe.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
   ; and pe.fin_class_cd in (mf_cs354_BCBS_CD, mf_cs354_COMMERCIALCONTRACT_CD, mf_cs354_MEDICAREREPLACEMENT_CD,
   ;                         mf_cs354_MEDICAID_CD, mf_cs354_MRHEMPLOYEES_CD,mf_cs354_WORKERSCOMPENSATION_CD,
   ;                         mf_cs354_MEDICARE_CD,mf_cs354_COMMERCIALNONCONTRACT_CD)
;    and pe.charge_balance != 0.0
    and pe.fin_class_cd not in (mf_cs354_CLIENT_CD, mf_cs354_SELFPAY_CD)
    and exists (select a.acct_id from account a where a.acct_id = pe.acct_id
                   and a.active_ind = 1
                   and a.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
                   and a.acct_sub_type_cd != mf_cs20849_CLIENT_CD
                   )
join be
  where be.billing_entity_id = pe.billing_entity_id
    and be.active_ind = 1
    and be.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and be.be_name_key in ('MAURYREGIONALMEDICALCENTER','MARSHALLMEDICALCENTER','WAYNEMEDICALCENTER')
join ea
  where ea.encntr_id = pe.encntr_id
    and ea.active_ind = 1
    and ea.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and ea.encntr_alias_type_cd = mf_cs319_FIN_CD
join e
  where e.encntr_id = pe.encntr_id
    and e.active_ind = 1
    and e.end_effective_dt_tm > cnvtdatetime(curdate,curtime3)
    and e.encntr_type_cd not in (mf_cs71_PREREG_CD, mf_cs71_CLIENT_CD)
order by br.corsp_activity_id, pe.pft_encntr_id, ea.beg_effective_dt_tm asc
head report
  m_rec->l_cnt = 0
head br.corsp_activity_id
  m_rec->l_cnt = m_rec->l_cnt + 1
  stat = alterlist(m_rec->qual,m_rec->l_cnt)
  m_rec->qual[m_rec->l_cnt].f_encntr_id = pe.encntr_id
  m_rec->qual[m_rec->l_cnt].f_pe_encntr_id = pe.pft_encntr_id
  m_rec->qual[m_rec->l_cnt].s_ucrn = trim(br.bill_nbr_disp,3)
  if (pe.recur_ind = 1)
    m_rec->qual[m_rec->l_cnt].s_fin = concat(trim(cnvtalias(ea.alias,ea.alias_pool_cd),3),'-',trim(cnvtstring(pe.recur_seq),3))
  else
    m_rec->qual[m_rec->l_cnt].s_fin = trim(cnvtalias(ea.alias,ea.alias_pool_cd),3)
  endif
 ; m_rec->qual[m_rec->l_cnt].s_med_service = getAlias(e.med_service_cd, mf_cs73_HL7STDSRC_CD)
  if (e.encntr_type_cd in (mf_cs71_INPATIENT_CD, mf_cs71_SWINGBED_CD))
    m_rec->qual[m_rec->l_cnt].s_med_service = 'I'
  else
    m_rec->qual[m_rec->l_cnt].s_med_service = 'O'
  endif
  if (be.be_name_key = 'MAURYREGIONALMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'MRH'
  elseif (be.be_name_key = 'MARSHALLMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'MMC'
  elseif (be.be_name_key = 'WAYNEMEDICALCENTER')
    m_rec->qual[m_rec->l_cnt].s_bill_entity = 'WMC'
  endif
with nocounter
 
;call echorecord(m_rec)
 
 
;open file
set stat = cclio("OPEN",frec)
 
;write header row
 
set frec->file_buf = build('"Claim Number",'
                        ,'"Patient ID",'
                        ,'"Service Category",'
                        ,'"Provider Org/Group Code"'
                        ,char(13),char(10))
set stat = cclio("WRITE",frec)
 
 
;write data
for (ml_idx1 = 1 to m_rec->l_cnt)
  set frec->file_buf = build('"',m_rec->qual[ml_idx1].s_ucrn
                            ,'","',m_rec->qual[ml_idx1].s_fin
                            ,'","',m_rec->qual[ml_idx1].s_med_service
                            ,'","',m_rec->qual[ml_idx1].s_bill_entity
                            ,'"',char(13),char(10))
  set stat = cclio("WRITE",frec)
endfor
 
;close file
set stat = cclio("CLOSE",frec)
 
/**************************************************************
; DVDev DEFINED SUBROUTINES
**************************************************************/
subroutine GetAlias(f_cv, f_contrib_sys_cd)
  declare ml_cv_pos = i4 with noconstant(0)
  declare ml_cs_pos = i4 with noconstant(0)
  declare ml_idx = i4 with noconstant(0)
  declare mf_code_set = f8 with noconstant(0)
  declare s_ret_val = vc with noconstant(concat("CD::",trim(cnvtstring(f_cv,20),3)))
 
  set mf_code_set = uar_get_code_set(f_cv)
 
  if(mf_code_set > 0)
    set ml_cs_pos = locatevalsort(ml_idx,1,m_out_alias->l_cs_cnt,mf_code_set,m_out_alias->qual[ml_idx].f_code_set)
  else
    return(s_ret_val)
  endif
 
  set ml_cv_pos = locatevalsort(ml_idx,1,m_out_alias->qual[ml_cs_pos].l_cv_cnt, f_cv,
                  m_out_alias->qual[ml_cs_pos].qual[ml_idx].f_code_value, f_contrib_sys_cd,
                  m_out_alias->qual[ml_cs_pos].qual[ml_idx].f_contrib_sys_cd)
 
  if(ml_cv_pos > 0)
    set s_ret_val = trim(m_out_alias->qual[ml_cs_pos].qual[ml_cv_pos].s_alias,3)
  endif
 
  return(s_ret_val)
end
 
subroutine LoadOutAlias(s_code_set, f_contrib_sys_cd)
  declare l_ret_val = i2 with protect, noconstant(0)
  ;declare l_cs_cnt = i4 with protect, noconstant(0)
  declare l_cv_cnt = i4 with protect, noconstant(0)
 
  if(f_contrib_sys_cd > 0.0)
    select into "nl:"
      from code_value_outbound cvo
     where parser(build("cvo.code_set in (",s_code_set,")"))
       and cvo.contributor_source_cd = f_contrib_sys_cd
    order by cvo.code_set, cvo.code_value
   ; head report
   ;   m_out_alias->l_cs_cnt = 0
    head cvo.code_set
      m_out_alias->l_cs_cnt = m_out_alias->l_cs_cnt + 1
      stat = alterlist(m_out_alias->qual,m_out_alias->l_cs_cnt)
      m_out_alias->qual[m_out_alias->l_cs_cnt].f_code_set = cvo.code_set
      m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt = 0
    detail
      m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt = m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt  + 1
      stat = alterlist(m_out_alias->qual[m_out_alias->l_cs_cnt]->qual,m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt)
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].f_code_value = cvo.code_value
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].s_alias = cvo.alias
      m_out_alias->qual[m_out_alias->l_cs_cnt].qual[m_out_alias->qual[m_out_alias->l_cs_cnt].l_cv_cnt].f_contrib_sys_cd =
                                                                                                           cvo.contributor_source_cd
    with nocounter
 
    if(m_out_alias->l_cs_cnt > 0)
      set l_ret_val = 1
    endif
  endif
  return(l_ret_val)
end
 
 
#EXIT_SCRIPT
end
go
 
